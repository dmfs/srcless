plugins {
    id 'org.dmfs.gitversion' version '0.5.0'
}

gitVersion {
    githubRepo "dmfs/srcless"
    changes {
        are major when {
            commitMessage contains("(?i)#major\\b")
        }
        are major when {
            commitMessage contains("(?i)#break\\b")
        }
        are minor when {
            commitMessage referencesGithubIssue { issue -> issue.labels?.every { it.name != "bug" } }
        }
        are patch when {
            commitMessage referencesGithubIssue { issue -> issue.labels?.any { it.name == "bug" } }
        }
        are minor when {
            commitMessage contains("(?i)\\b(implement(s|ed)?|close[sd]?) #\\d+\\b")
        }
        are patch when {
            commitMessage contains("(?i)\\b(fix(e[sd])?|resolve[sd]?) #\\d+\\b")
        }
        otherwise patch
    }
    preReleases {
        on ~/master|main/ use { "beta" }
        on ~/(.*\/)?(?<name>.*)/ use { "alpha-${group('name')}.1" }
    }
    releaseBranchPattern ~/master$|main$/
}

allprojects {
    group 'org.dmfs'
    repositories {
        mavenCentral()
    }
}
